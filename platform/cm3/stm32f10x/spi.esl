/*
 * STM32F10x SPI.
 *
 * Copyright (c) 2012, Brian G. Lucas.  See LICENSE file.
 * $Id: spi.esl 326 2012-02-24 22:06:49Z bgl $
 */

package spi
{
    /*
     * SPI registers can be accessed as 16-bit or 32-bit words
     * The layout below is for 16-bit access.
     */
    type ModeT:	// ckpol ckpha
    (   MODE0,	//   0     0
	MODE1,	//   0     1
	MODE2,	//   1     0
	MODE3	//   1     1
    );
    type CR1:
    {   mode:   ModeT;			// .00-01
	master: boolean;		// .02
	brdiv:  0..7;			// .03-05
	enable: boolean;		// .06
	format: (MSB,LSB);		// .07
	ssi:    0..1;			// .08
	ssm:    boolean;		// .09
	rxonly: boolean;		// .10
	size:   (B8, B16);		// .11
	crcnxt: boolean;		// .12
	crcenb: boolean;		// .13
	duplex: (FULL,_,RECV,XMIT);	// .14-15
    }: packed, lsb;
    type SPIReg:
    {   cr1: CR1: out, align(4);					// 000
    	cr2:								// 004
    	{   rxdma:  boolean;		// .00
    	    txdma:  boolean;		// .01
    	    ssoe:   boolean;		// .02
    	    _:      0..0b11;		// .03-04
    	    errie:  boolean;		// .05
    	    rxneie: boolean;		// .06
    	    txeie:  boolean;		// .07
    	    _:      0..0xFF;		// .08-15
    	}: packed, lsb, out, align(4);
	sr:								// 008
	{   rxne:   boolean: ro;	// .00
	    txe:    boolean: ro;	// .01
	    chside: (LEFT,RIGHT): ro;	// .02
	    udr:    boolean: ro;	// .03
	    crcerr: boolean;		// .04
	    modf:   boolean: ro;	// .05
	    ovr:    boolean: ro;	// .06
	    bsy:    boolean: ro;	// .07
    	    _:      0..0xFF;		// .08-15
    	}: packed, lsb, in, out, align(4);
        dr: _uint16: in, out, align(4);					// 00C
        crcpr: _uint16: out, align(4);					// 010
        rxcrc: _uint16: ro, in, align(4);				// 014
        txcrc: _uint16: ro, in, align(4);				// 018
        i2scfg:								// 01C
        {   chlen:  (B16,B32);		// .00
            datlen: (B16,B24,B32,_);	// .01-02
            ckpol:  (LO,HI);		// .03
            std:    (PHIL,MSB,LSB,PCM); // .04-05
            _:      0..1;		// .06
            pcmsync:boolean;		// .07
            cfg:    (STx,SRx,MTx,MRx);	// .08-09
            i2se:   boolean;		// .10
            mode:   (SPI, I2S);		// .11
    	    _:      0..0b1111;		// .12-15
     	}: packed, lsb, out, align(4);
	i2spr:								// 020
	{   div:    0..0xFF;		// .00-07
	    odd:    0..1;		// .08
	    mckoe:  boolean;		// .09
    	    _:      0..0x3F;		// .10-15
     	}: packed, lsb, out, align(4);            
    };
    type Spi: @SPIReg;
    const devices: [3]
    {   addr:   Spi;
        vector: _uint8;
	dev:    sysctl.Device;
        bus:    sysctl.Bus;
        isi2c:  boolean;
    } =
    {   { 0x4001_3000, 35, SPI1, APB2, false },
	{ 0x4000_3800, 36, SPI2, APB1, false },
	{ 0x4000_3C00, 51, SPI3, APB1, false }
    };

    proc (spi: Spi) Send(data: _uint16)
    {
    	while !spi.sr.txe do {}
    	spi.dr = data;
    }

    proc (spi: Spi) Recv(): _uint16
    {
    	while !spi.sr.rxne do {}
    	return spi.dr;
    }

    proc (spi: Spi) Enable(enb: boolean)
    {
	spi.cr1.enable = enb;
    }

    proc (spi: Spi) SetFrequency(freq: _uint)
    {
	var clk: _uint;
	var div: _uint;

	if spi == devices[0].addr then
	    clk = sys.P2Clock;
	else
	    clk = sys.P1Clock;
	clk = clk/2;		// pre-divide by 2
	div = 0;
	while div < 8 && clk > freq do
	{   clk = clk/2;
	    div += 1;
	}
	if div >= 8 then return;
	spi.cr1.brdiv = div;
    }

    proc Init(n: _uint, how: CR1, poly: _uint16): Spi
    {   var spi: Spi;
	var cr1: CR1;

	n -= 1;	// make zero based
	if n > 2 then return 0;
	sysctl.ClockEnable(devices[n].dev);
	sysctl.DeviceReset(devices[n].dev);
	spi = devices[n].addr;	
	cr1 = how;
	if cr1.master then cr1.ssi = 1;
	spi.cr1 = cr1;
	spi.crcpr = poly;
	return spi;
    }

}

